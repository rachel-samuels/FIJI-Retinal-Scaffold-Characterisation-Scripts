// Step 1: Subtract Background
run("Subtract Background...", "rolling=900 stack");

// Step 2: Run CLAHE Enhancement on all images in the stack
blocksize = 127;
histogram_bins = 256;
maximum_slope = 3;
mask = "*None*";
fast = true;
process_as_composite = true;

getDimensions(width, height, channels, slices, frames);
isComposite = channels > 1;
parameters =
  "blocksize=" + blocksize +
  " histogram=" + histogram_bins +
  " maximum=" + maximum_slope +
  " mask=" + mask;

if (fast) 
  parameters += " fast_(less_accurate)";

if (isComposite && process_as_composite) {
  parameters += " process_as_composite";
  channels = 1;
}

for (f=1; f<=frames; f++) {
  Stack.setFrame(f);
  for (s=1; s<=slices; s++) {
    Stack.setSlice(s);
    for (c=1; c<=channels; c++) {
      Stack.setChannel(c);
      run("Enhance Local Contrast (CLAHE)", parameters);
    }
  }
}

// Step 3: Despeckle the stack
run("Despeckle", "stack");

// Step 4: Apply Gaussian Blur 3D with specified sigma values
run("Gaussian Blur 3D...", "x=2 y=2 z=2");

// Step 5: Auto-thresholding using the Huang method
setAutoThreshold("Huang dark");
setOption("BlackBackground", false);

// Step 6: Convert to Mask using Huang method
run("Convert to Mask", "method=Huang background=Dark calculate");
